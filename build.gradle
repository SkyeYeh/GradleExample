group 'com.skyeYeh'
version '1.0-SNAPSHOT'

/* 引用 java plugin 獲得編譯 java 專案相關的 task $ */
apply plugin: 'java'

/* 引用 application plugin 獲得執行 java 專案相關的 task $ */
apply plugin: 'application'

/* 執行 application plugin 用到的參數 $ */
mainClassName = "com.skyeYeh.gradleExample.HelloWorld"

/* 設定 maven repository server $ */
repositories {
    mavenCentral()
}

/* 宣告專案的相依函式庫 $ */
dependencies {
    compile group: 'commons-logging', name: 'commons-logging', version: '1.1.1'
    compile group: 'log4j', name: 'log4j', version: '1.2.16'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

/**
 * Example1。
 */
task example1 << {
    /* def 是 Groovy 內的萬用型別，不管是物件還是原生變數甚至 void 都能用它代替，如果看不慣 def 的寫法，用原先 java 的宣告方式也行 */
    String message = "hello groovy closure"
    def codeBlock = { println message }

    /* 加上小括號就當成 method 般呼叫 */
    codeBlock()

    /* 也能呼叫 Closure 物件的 call() 方法 */
    codeBlock.call()
}

/**
 * Example2。
 * 當 Closure 內無法識別方法或參數時，會透過委派物件處理。
 */
class JavaHello {
    def sayHello() { println "Hello Java" }
}

class GroovyHello {
    def sayHello() { println "Hello Groovy" }
}

task example2 << {
    def codeBlock = { sayHello() }

    /* 在可見的 scope 內依然沒有 sayHello() 方法 */
    /* Could not find method sayHello() for arguments [] on root project 'gradleExample'. */
    // codeBlock()

    /* JavaHello 這件事就交給你吧！ */
    codeBlock.delegate = new JavaHello();

    /* 於是印出了 Hello Java */
    codeBlock()

    /* GroovyHello 這件事就交給你吧！ */
    codeBlock.delegate = new GroovyHello()

    /* 於是印出了 Hello Groovy */
    codeBlock()
}

/**
 * example3。
 */
task "example3A" << {
    println("Hi example3A");
}

task("example3B").doLast({
    println("Hi example3B");
})
